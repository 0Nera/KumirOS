использовать Рисователь

цел ширина = 800
цел высота = 600

цвет небоЦвет; небоЦвет := голубой

вещ сфераX = 0
вещ сфераY = 0
вещ сфераZ = 5
вещ сфераРадиус = 3
цвет сфераЦвет; сфераЦвет := красный

вещ лампаX = 5
вещ лампаY = -3
вещ лампаZ = -2

алг
нач
новый лист(ширина, высота, серый)

цел y, x

вещ xv, yv, zv

zv := 1.0

нц для y от 0 до высота - 1
нц для x от 0 до ширина - 1
xv := ((x - ширина / 2) * 8.0 / ширина)
yv := ((y - высота / 2) * 6.0 / высота)
пиксель(x, y, выпуститьЛуч(0, 0, 0, xv, yv, zv))
кц
вывод "СТРОКА ", y, нс
кц

кон

алг цвет выпуститьЛуч(вещ ox, oy, oz, dx, dy, dz)
нач
вещ t
t := попадениеВСферу(ox, oy, oz, dx, dy, dz)

вещ tx, ty, tz
tx := ox + dx * t
ty := oy + dy * t
tz := oz + dz * t

вещ свет

если t <> -1
то
свет := освещенностьСферы(tx, ty, tz)
знач := умнЦвет(сфераЦвет, свет)
иначе 
знач := небоЦвет
все
кон

алг вещ попадениеВСферу(вещ ox, oy, oz, dx, dy, dz)
нач
вещ ocx, ocy, ocz
ocx := ox - сфераX
ocy := oy - сфераY
ocz := oz - сфераZ

вещ a, hb, c
a := dot(dx, dy, dz, dx, dy, dz)
hb := dot(ocx, ocy, ocz, dx, dy, dz)
c := dot(ocx, ocy, ocz, ocx, ocy, ocz) - сфераРадиус*сфераРадиус

вещ d
d := hb*hb - a*c;

если d < 0
то
знач := -1
иначе
знач := (-hb - sqrt(d)) / a
все
кон

алг вещ освещенностьСферы(вещ tx, ty, tz)
нач
вещ nx, ny, nz
nx := tx - сфераX
ny := ty - сфераY
nz := tz - сфераZ

нормализацияВектора(nx, ny, nz, nx, ny, nz)

вещ ldx, ldy, ldz
ldx := лампаX - tx
ldy := лампаY - ty
ldz := лампаZ - tz

нормализацияВектора(ldx, ldy, ldz, ldx, ldy, ldz)

знач := dot(nx, ny, nz, ldx, ldy, ldz) / (длинаВектора(nx, ny, nz) * длинаВектора(ldx, ldy, ldz))
кон

алг вещ dot(вещ x1, y1, z1, x2, y2, z2)
нач
знач := x1 * x2 + y1 * y2 + z1 * z2
кон

алг вещ длинаВектора(вещ x, y, z)
нач
знач := sqrt(x * x + y * y + z * z)
кон

алг нормализацияВектора(вещ x, y, z, рез вещ xn, yn, zn)
нач
вещ длина 
длина := длинаВектора(x, y, z)
xn := x / длина
yn := y / длина
zn := z / длина
кон

алг цвет умнЦвет(цвет цв, вещ множитель)
нач
цел r, g, b
разложить в RGB(цв, r, g, b)
r := int(r * множитель)
g := int(g * множитель)
b := int(b * множитель)
знач := RGB(r, g, b)
кон
